
config {
  type: "table",
  tags: ["SolarSystemBody"],
  schema: "SolarSystemBody", 
  uniqueKey: ["PlanetId"],
  description: "This table stores physical and orbital characteristics of planets in our solar system sourced from the System Solaire open-source API at https://api.le-systeme-solaire.net/en/ ",
  columns: {
    PlanetId: "Unique identifier for each planet generated by ordering their perihelion distances",
    PlanetName: "English name of the planet",
    Gravity: "Surface gravity of the planet measured in meters per second squared (m/s²)",
    Mass: "Total mass of the planet measured in kilograms (kg)",
    Volume: "Total volume of the planet measured in cubic kilometers (km³)",
    Density: "Average density of the planet measured in grams per cubic centimeter (g/cm³)",
    AverageSurfaceTemperature: "Mean surface temperature of the planet measured in Kelvin (K)",
    AxialTilt: "Angle between the planet''s rotational axis and its orbital axis measured in degrees",
    Eccentricity: "Measure of how much the planet''s orbit deviates from circular, where 0 is perfectly circular",
    Perihelion: "Closest distance between the planet and the Sun during its orbit measured in kilometers (km)",
    Aphelion: "Farthest distance between the planet and the Sun during its orbit measured in kilometers (km)"
  }
}

select
  -- Use perihelion value for each planet in window func to generate a unique PlanetId
  row_number() over (order by perihelion) as PlanetId
  , englishName as PlanetName
  , gravity as Gravity
  , mass.MassValue as Mass
  , vol.volValue as Volume
  , density as Density
  , avgTemp as AverageSurfaceTemperature
  , axialTilt as AxialTilt
  , eccentricity as Eccentricity
  , perihelion as Perihelion 
  , aphelion as Aphelion
from ${ref("RawPlanet")}
